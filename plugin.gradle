buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.0.1'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'js'

javascript {
    source {
        custom {
            js {
                srcDir "src/test/resources"
                include "file*.js"
            }
        }
    }
}

// Combine JS files
task combine(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/all.js")
}

// Minify with Google Closure Compiler
task minify(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
    source = file("${buildDir}/all.js")
    dest = file("${buildDir}/all-min.js")
    closure {
        warningLevel = 'VERBOSE'
        compilationLevel = 'SIMPLE_OPTIMIZATIONS'
        externs = files("src/test/resources/externs.js")
    }
}

// GZip it!
task gzip(type: com.eriwen.gradle.js.tasks.GzipJsTask) {
    source = file("${buildDir}/all-min.js")
    dest = file("${buildDir}/all-min.js")
}

task jshintz(type: com.eriwen.gradle.js.tasks.JsHintTask) {
    source = javascript.source.custom.js.files
}

task jsdocz(type: com.eriwen.gradle.js.tasks.JsDocTask) {
    source = javascript.source.custom.js.files
    destinationDir = file("${buildDir}/jsdoc")
}

task props(type: com.eriwen.gradle.js.tasks.Props2JsTask) {
    source = file("${projectDir}/src/test/resources/test.properties")
    dest = file("${buildDir}/props.jsonp")
    project.props.type = 'jsonp'
    project.props.functionName = 'fn'
}
